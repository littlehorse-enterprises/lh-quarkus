pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    plugins {
        id "io.quarkus" version "${quarkusVersion}"
        id "io.quarkus.extension" version "${quarkusVersion}"
        id "com.diffplug.spotless" version "${spotlessVersion}"
        id "io.github.gradle-nexus.publish-plugin" version "${nexusVersion}"
    }
}
rootProject.name = "littlehorse-quarkus-extensions"

extensionProjects.split(',').each {
    include("littlehorse-$it")
    project(":littlehorse-$it").projectDir = file("extensions/littlehorse-$it/runtime")

    include("littlehorse-$it-deployment")
    project(":littlehorse-$it-deployment").projectDir = file("extensions/littlehorse-$it/deployment")
}

exampleProjects.split(',').each {
    include "example-$it"
    project(":example-$it").projectDir = file("examples/$it")
}

otherProjects.split(',').each {
    include "$it"
    project(":$it").projectDir = file("$it")
}
